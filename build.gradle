buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = '1.6' // patch affects only Java 8 but aims for compatibility with Java 6+
targetCompatibility = '1.6' // in case it is used in software that supports a broad version range
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    compile "asm:asm:3.1"
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

jar {
    manifest {
        attributes 'Main-Class': mainClass
        attributes 'Premain-Class': mainClass
        attributes 'Implementation-Version': version
        attributes 'Implementation-URL': projectUrl
        attributes 'Build-Date': new Date().toString()
        attributes 'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']})"
    }
}
jar.dependsOn check

shadowJar {
    from('NOTICE') { // only required in shadowed JARs that include dependency binaries
        into 'META-INF'
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    //from sourceSets.main.allSource -- excluded: see tasks.withType(Jar) below...
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Jar) {
    // applies to all distributions in order to be compliant with GPLv2
    from sourceSets.main.allSource
    from('LICENSE') {
        into 'META-INF'
    }
}

artifacts {
    archives shadowJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact shadowJar
            artifact sourceJar {
                classifier "sources"
            }
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'JavaAgent that patches the HALF_UP NumberFormat bug in JDK8')
                root.appendNode('url', projectUrl)
                root.appendNode('inceptionYear', '2014')

                def org = root.appendNode('organization')
                org.appendNode('name', 'PROS, Inc.')
                org.appendNode('url', 'http://www.pros.com/')

                def scm = root.appendNode('scm')
                scm.appendNode('url', projectUrl)
                scm.appendNode('connection', 'scm:https://github.com/PROSPricing/jdk8patch-halfupround.git')

                def lic = root.appendNode('licenses').appendNode('license')
                lic.appendNode('name', 'GNU General Public License Version 2 (GPLv2) with Classpath Exception')
                lic.appendNode('url', 'http://openjdk.java.net/legal/gplv2+ce.html')
                lic.appendNode('distribution', 'repo')
            }
        }
    }
    repositories {
        maven {
            url project.version.endsWith('-SNAPSHOT') ? ossProsRepoSnapshotUrl : ossProsRepoReleaseUrl
            credentials {
                username = ossProsRepoUsername
                password = ossProsRepoPassword
            }
        }
    }
}
